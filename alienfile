use alienfile;

my $test_program = <<'EOF';
#include <stdio.h>
#include <libxml/xmlversion.h>
#include <libxml/parser.h>
#include <libxml/tree.h>

int
main(int argc, char *argv[])
{
  xmlDoc *doc = NULL;
  /*
   * xmlRead should fail, but having this call ensures
   * that the we have access to the symbols in -lxml2
   * (LIBXML_DOTTED_VERSION below is in the header file)
   */
  doc = xmlReadFile("foo.xml", NULL, 0);
  printf("xml version = '%s'\n", LIBXML_DOTTED_VERSION);
}
EOF

my @try_flags = (
  { cflags => '',                             libs => '-lxml2' },
  { cflags => '-I/usr/include/libxml2',       libs => '-lxml2' },
  { cflags => '-I/usr/local/include/libxml2', libs => '-lxml2' },
);

plugin 'PkgConfig' => 'libxml-2.0';

configure { requires 'Alien::Build' => '1.18' };

probe [
  ['xml2-config', '--version' => \'%{.install.my_version}'],
  ['xml2-config', '--cflags'  => \'%{.install.my_cflags}'],
  ['xml2-config', '--libs'    => \'%{.install.my_libs}'],
];

plugin 'Probe::CBuilder' => (
  cflags  => $_->{cflags},
  libs    => $_->{libs},
  program => $test_program,
  version => qr{xml version = '(.*?)'},
) for @try_flags;
plugin 'PkgConfig::MakeStatic' => ();

my %bad_versions = map { $_ => 1 } (
  '2.4.22',
  '2.4.25',  # broken XPath
  '2.4.28',  # unsupported, may work fine with earlier XML::LibXML versions
  '2.4.29',  # broken
  '2.4.30',  # broken
  '2.5.0',   # unsupported
  '2.5.1',   # all pre 2.5.4 version have broken attr output
  '2.5.5',   # tests pass, but known as broken
  '2.5.11',  # will partially work
  '2.6.0',   # unsupported
  '2.6.4',   # schema error
  '2.6.5',   # broken xincludes
  '2.6.15',
  '2.6.19',  # broken c14n
  '2.6.20',  # broken schemas
  '2.6.25',  # broken XPath
  '2.7.1',   # broken release, broken utf-16
  '2.9.4',   # schema regression
);

meta->around_hook(
  probe => sub {
    my $orig = shift;
    my $build = shift;
    my $install_type = $orig->($build, @_);
    # if the system type is install, make sure the version isn't on the
    # blacklist.
    # Honor $ENV{FORCE} used in the Makefile.PL for XML-LibXML to allow
    # versions on the blacklist.
    if($install_type eq 'system' && !$ENV{FORCE})
    {
      # the first value is generated by the Probe::CBuilder plugin,
      # the second is from this alienfile.  Either could find a system libxml.
      # TODO: the capability to do this should exist in-core for Alien-Build.
      my $version = $build->install_prop->{plugin_prfobe_cbuilder_gather}->{version} ||
                    $build->install_prop->{my_version} ||
                    die 'TODO: pkg-config plugins do not set version :/';
      return 'share' if $bad_versions{$version};
    }
    $install_type;
  },
);

plugin 'Prefer::BadVersion' => [ keys %bad_versions ];

share {

  meta->prop->{out_of_source} = 1;

  plugin Download => (
    url     => 'ftp://xmlsoft.org/libxml2/',
    version => qr/^libxml2-sources-([\d\.]+)\.tar\.gz$/,
  );

  plugin Extract => 'tar.gz';
  plugin 'Build::Autoconf' => ();

  build [
    '%{configure} --prefix=%{.install.autoconf_prefix} --without-python --disable-shared --enable-static',
    '%{make}',
    '%{make} install',
  ];
  
  plugin 'PkgConfig::MakeStatic' => ();

};

sys {

  gather sub {
    my($build) = @_;
    if($build->install_prop->{my_libs})
    {
      $build->runtime_prop->{$_} = $build->install_prop->{"my_$_"} for qw( version cflags libs );
    }
  };

};
