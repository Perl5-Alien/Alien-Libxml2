use alienfile;

my $test_program = <<'EOF';
#include <stdio.h>
#include <libxml/xmlversion.h>

int
main(int argc, char *argv[])
{
  printf("xml version = '%s'\n", LIBXML_DOTTED_VERSION);
}
EOF

my @try_flags = (
  { cflags => '', libs => '-lxml2' },
  { cflags => '-I/usr/include/libxml2', libs => '-lxml2' },
  { cflags => '-I/usr/local/include/libxml2', libs => '-lxml2' },
);

plugin 'PkgConfig' => 'libxml-2.0';
plugin 'Probe::CBuilder' => (
  cflags  => $_->{cflags},
  libs    => $_->{libs},
  program => $test_program,
  version => qr{xml version = '(.*?)'},
) for @try_flags;
plugin 'PkgConfig::MakeStatic' => ();

share {

  meta->prop->{out_of_source} = 1;

  plugin Download => (
    url     => 'ftp://xmlsoft.org/libxml2/',
    version => qr/^libxml2-sources-([\d\.]+)\.tar\.gz$/,
  );

  plugin Extract => 'tar.gz';
  plugin 'Build::Autoconf' => ();

  build [
    '%{configure} --prefix=%{alien.install.autoconf_prefix} --without-python --disable-shared --enable-static',
    '%{make}',
    '%{make} install',
  ];
  
  plugin 'PkgConfig::MakeStatic' => ();

};
